DISTRO_NAME="Toradex Easy Installer"
DISTRO_VERSION="2.4"

DISTRO_FEATURES="acl ipv4 ipv6 keyboard usbhost usbgadget ext2 largefile wayland wifi xattr"
DISTRO_FEATURES_BACKFILL_CONSIDERED="pulseaudio bluez5"

DISTROOVERRIDES .= ":tdx"

TCLIBC = "glibc"
IMAGE_LINGUAS = ""

INHERIT += "buildhistory"
BUILDHISTORY_COMMIT = "1"

INITRAMFS_IMAGE = "tezi-initramfs"
INITRAMFS_FSTYPES = "squashfs"
INITRAMFS_MAXSIZE = "160000"
FIT_SUPPORTED_INITRAMFS_FSTYPES = "${INITRAMFS_FSTYPES}"

VIRTUAL-RUNTIME_dev_manager = "udev"
VIRTUAL-RUNTIME_login_manager = ""
VIRTUAL-RUNTIME_init_manager = "tezi-init"

MACHINE_EXTRA_RRECOMMENDS:remove = "udev-rules-imx"
MACHINE_EXTRA_RRECOMMENDS:append = " udev-rules-tezi"

# Avoid egl build dependency, which is not compatible with imx7 and we don't need it anyway
# Avoid imx variant drm dependency, we use regular drm
BBMASK += " \
    /meta-freescale/dynamic-layers/qt5-layer/recipes-qt/qt5/ \
    /meta-freescale/recipes-graphics/drm/ \
    /meta-freescale/recipes-graphics/wayland/weston-init.bbappend \
    /meta-toradex-nxp/recipes-graphics/drm \
    /meta-toradex-bsp-common/recipes-bsp/u-boot/u-boot-distro-boot.bb \
"

# Qt5 extra configuration
PACKAGECONFIG:append:pn-qtbase = " linuxfb no-opengl xkbcommon fontconfig"
PACKAGECONFIG:remove:pn-qtwayland = "wayland-egl wayland-server"

MACHINE_SOCARCH_FILTER:append:mx8-generic-bsp = " qtbase"

# Get rid of EGL and GLESv2 in cairo
PACKAGECONFIG:remove:pn-cairo = "egl glesv2"

IMX_DEFAULT_BSP = "mainline"
IMX_DEFAULT_BSP:aarch64 = "nxp"

PREFERRED_PROVIDER_virtual/dtb ?= "device-tree-overlays"

PREFERRED_PROVIDER_u-boot-default-script = "u-boot-distro-boot"
PREFERRED_VERSION_u-boot-distro-boot = "1.1"

PREFERRED_VERSION_weston:forcevariable = "9.0.0"
PREFERRED_VERSION_wayland:forcevariable = "1.20.0"
PREFERRED_VERSION_wayland-protocols:forcevariable = "1.25"
PREFERRED_VERSION_libdrm:forcevariable = "2.4.110"

# Remove python from tezi-devel (gets pulled by gdb bbappend in meta-qt5)
PACKAGECONFIG:remove:pn-gdb = "python"

# Make sure we use OpenSSL only
PACKAGECONFIG:pn-curl = "ipv6 ssl proxy zlib"
PACKAGECONFIG:pn-wpa-supplicant = "openssl"

# Enable acl support in GNU tar
PACKAGECONFIG:pn-tar = "acl"

# Add License files to image
COPY_LIC_DIRS = "1"
COPY_LIC_MANIFEST = "1"

# Some Toradex recipes like linux-toradex, u-boot-toradex, device-tree-overlays,
# qt-tezi, qt-tezictl are appending SCM hashes to PV, version
# numbers going backwards are not a problem as we don't provide
# binary feeds.
#
# This avoids some annoying errors as follows:
# | QA Issue: Package version went backwards which would break package feeds
ERROR_QA:remove = "version-going-backwards"

# Explicitly use fitimage
KERNEL_CLASSES:append = " kernel-fitimage"
KERNEL_IMAGETYPE:forcevariable = "fitImage"
KERNEL_ALT_IMAGETYPE = "zImage"
KERNEL_ALT_IMAGETYPE:mx8-generic-bsp = "Image"

# Make sure kernel does not get deployed into rootfs
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""

# No firmwares required for a simple Qt UI
MACHINE_FIRMWARE:remove = "firmware-imx-vpu-imx6q firmware-imx-vpu-imx6d firmware-imx-vpu-imx8 firmware-imx-epdc linux-firmware-ath10k"

# No TDX firmware for tezi image
MACHINE_EXTRA_RDEPENDS:remove:apalis-tk1 = "tdx-k20-fw"

MACHINE_EXTRA_RDEPENDS:append:colibri-imx6ull = " mtd-utils-ubifs mtd-utils kobs-ng"
MACHINE_EXTRA_RDEPENDS:append:colibri-imx7 = " mtd-utils-ubifs mtd-utils kobs-ng"

UBOOT_MAKE_TARGET:apalis-imx6 = "u-boot.img SPL"
UBOOT_MAKE_TARGET:colibri-imx6 = "u-boot.img SPL"
# work around colibri-imx7/colibri-imx6ull being overridden a second time
# later on in machine config
UBOOT_MAKE_TARGET:colibri-imx6ull:forcevariable = "u-boot-nand.imx"
UBOOT_MAKE_TARGET:colibri-imx7:forcevariable = "u-boot-nand.imx"

UBOOT_SUFFIX:apalis-imx6 = "img"
UBOOT_SUFFIX:colibri-imx6 = "img"
SPL_BINARY:apalis-imx6 = "SPL"
SPL_BINARY:colibri-imx6 = "SPL"

UBOOT_SDP_SUPPORT = "0"
UBOOT_SDP_SUPPORT:apalis-imx6 = "1"
UBOOT_SDP_SUPPORT:colibri-imx6 = "1"
UBOOT_SDP_SUPPORT:colibri-imx6ull = "1"
UBOOT_SDP_SUPPORT:colibri-imx7 = "1"
MACHINE_BOOT_FILES:append = " ${@oe.utils.ifelse(d.getVar('UBOOT_SDP_SUPPORT') == '1', 'boot-tezi.scr boot-sdp.scr', 'boot-tezi.scr')}"
MACHINE_BOOT_FILES:append:apalis-imx8 = " hdmitxfw.bin "
MACHINE_BOOT_FILES:append:mx8-generic-bsp = " overlays.txt"
MACHINE_BOOT_FILES:append:colibri-imx6 = " overlays.txt"
MACHINE_BOOT_FILES:append:apalis-imx6  = " overlays.txt"

UBOOT_CONFIG:apalis-imx6 = "apalis-imx6-tezi"
UBOOT_CONFIG[apalis-imx6-tezi]="apalis_imx6_tezi_defconfig"

UBOOT_MACHINE_FW_UTILS:apalis-imx6="apalis_imx6_defconfig"

UBOOT_CONFIG:apalis-imx8 = "apalis-imx8-tezi"
UBOOT_CONFIG[apalis-imx8-tezi]="apalis-imx8_tezi_defconfig"

UBOOT_CONFIG:apalis-tk1 = "apalis-tk1-tezi"
UBOOT_CONFIG[apalis-tk1-tezi]="apalis-tk1_tezi_defconfig"
UBOOT_MACHINE:apalis-tk1 = ""
UBOOT_SUFFIX:apalis-tk1 = "bin"

UBOOT_CONFIG:colibri-imx6 = "colibri-imx6-tezi"
UBOOT_CONFIG[colibri-imx6-tezi]="colibri_imx6_tezi_defconfig"

UBOOT_MACHINE_FW_UTILS:colibri-imx6="colibri_imx6_defconfig"

UBOOT_MACHINE:colibri-imx6ull = ""

UBOOT_MACHINE_FW_UTILS:colibri-imx6ull="colibri-imx6ull_defconfig"

UBOOT_MACHINE_FW_UTILS:colibri-imx7="colibri_imx7_defconfig"
UBOOT_MACHINE_FW_UTILS:colibri-imx7-emmc="colibri_imx7_emmc_defconfig"

UBOOT_CONFIG:colibri-imx8x = "colibri-imx8x-tezi"
UBOOT_CONFIG[colibri-imx8x-tezi]="colibri-imx8x_tezi_defconfig"

UBOOT_CONFIG:verdin-imx8mm = "verdin-imx8mm-tezi"
UBOOT_CONFIG[verdin-imx8mm-tezi]="verdin-imx8mm_tezi_defconfig"

UBOOT_CONFIG:verdin-imx8mp = "verdin-imx8mp-tezi"
UBOOT_CONFIG[verdin-imx8mp-tezi]="verdin-imx8mp_tezi_defconfig"

TEZI_EXTERNAL_KERNEL_DEVICETREE_BOOT:apalis-imx6 = "apalis-imx6_vga-640x480_overlay.dtbo apalis-imx6_hdmi_overlay.dtbo"
TEZI_EXTERNAL_KERNEL_DEVICETREE_BOOT:colibri-imx6 = "colibri-imx6_vga-640x480_overlay.dtbo colibri-imx6_hdmi_overlay.dtbo"

UBOOT_DTB_LOADADDRESS:mx8-generic-bsp     = "0x87000000"
UBOOT_DTBO_LOADADDRESS:mx8-generic-bsp    = "0x870F0000"
UBOOT_DTB_LOADADDRESS_verdin  = "0x4A000000"
UBOOT_DTBO_LOADADDRESS_verdin = "0x4A0F0000"
UBOOT_DTB_LOADADDRESS:colibri-imx6 = "0x14200000"
UBOOT_DTBO_LOADADDRESS:colibri-imx6 = "0x142F0000"
UBOOT_DTB_LOADADDRESS:apalis-imx6 = "0x14200000"
UBOOT_DTBO_LOADADDRESS:apalis-imx6 = "0x142F0000"

UBOOT_DTB_LOADADDRESS:colibri-imx6ull = ""
UBOOT_DTB_LOADADDRESS:colibri-imx7 = ""
UBOOT_DTB_LOADADDRESS:colibri-imx7-emmc = ""
UBOOT_DTB_LOADADDRESS:apalis-tk1 = ""

require conf/distro/tdx_version.inc
