#@TYPE: Machine
#@NAME: Toradex Colibri iMX6ULL for use with Toradex Easy Installer
#@SOC: i.MX6ULL
#@DESCRIPTION: Machine configuration for Toradex Colibri iMX6 ULL SOM for use with Toradex Easy Installer
#@MAINTAINER: Max Krummenacher <max.krummenacher@toradex.com>

MACHINEOVERRIDES =. "mx6:mx6ul:mx6ull:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa7.inc

PREFERRED_PROVIDER_virtual/kernel_use-nxp-bsp ?= "linux-toradex"
KERNEL_DEVICETREE_forcevariable = "imx6ull-colibri-eval-v3.dtb imx6ull-colibri-emmc-eval-v3.dtb"
KERNEL_IMAGETYPE = "zImage"
# Do not install the kernel into the rootfs
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

PREFERRED_PROVIDER_u-boot ?= "u-boot-toradex"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-toradex"

UBOOT_MAKE_TARGET="u-boot-nand.imx"
UBOOT_CONFIG = "emmc rawnand recovery"
UBOOT_CONFIG[emmc] = "colibri-imx6ull-emmc_tezi_defconfig,,u-boot.imx"
UBOOT_CONFIG[rawnand] = "colibri-imx6ull_tezi_defconfig,,u-boot-nand.imx"
UBOOT_CONFIG[recovery] = "colibri-imx6ull_tezi-recovery_defconfig,,u-boot.imx"

UBOOT_ENTRYPOINT_colibri-imx6ull = "0x81000000"

# The payload offsets are given in number of 512 byte blocks.
OFFSET_BOOTROM_PAYLOAD = "2"

IMAGE_CLASSES += "image_type_tezi"
IMAGE_FSTYPES = "teziimg"

TORADEX_FLASH_TYPE = "emmc rawnand"
TORADEX_PRODUCT_IDS += "0062"
TORADEX_PRODUCT_IDS[0062] = "imx6ull-colibri-emmc-eval-v3.dtb,emmc"

TEZI_UBOOT_BINARY_EMMC = "u-boot.imx-emmc"
TEZI_UBOOT_BINARY_RAWNAND = "u-boot.imx-rawnand"

MACHINE_FEATURES += "screen usbgadget usbhost vfat ext2 alsa touchscreen wifi bluetooth 3g"
MACHINE_FIRMWARE_remove = "firmware-imx-epdc"
